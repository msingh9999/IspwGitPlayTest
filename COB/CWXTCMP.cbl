000300 IDENTIFICATION DIVISION.
000400 PROGRAM-ID.       CWXTCMP.
000500******************************************************************
000600*                                                                *
000700*****       C O M P U W A R E   C O R P O R A T I O N       ******
000800*                                                                *
000900*  THIS PROGRAM IS THE COBOL DEMO PROGRAM USED FOR XPEDITER/TSO  *
001000*  TRAINING SESSIONS.
001100*  TEST IMPORT
001200*  TEST XEBIA JEAN-DAVID                                         *
001300*                                                                *
001400*  INPUT FILE  - EMPLOYEE WAGE INFORMATION                       *
001500*              - STORED IN XPEDITER/TSO SAMPLIB (CWXTDATA)       *
001600*  OUTPUT FILE - EMPLOYEE COMPENSATION REPORT                    *
001700*              - REGIONAL SALES REPORT                           *
001800*                                                                *
001900*  RUN JCL     - STORED IN XPEDITER/TSO SAMPLIB (CWXTJCLC)       *
002000*                                                                *
002100******************************************************************
002200 ENVIRONMENT DIVISION.
002300 INPUT-OUTPUT SECTION.
002400 FILE-CONTROL.
002500     SELECT EMPLOYEE-FILE ASSIGN TO EMPFILE.
002600     SELECT REPORT-FILE   ASSIGN TO RPTFILE.
002700 DATA DIVISION.
002800 FILE SECTION.
002900 FD  EMPLOYEE-FILE
003000     LABEL RECORDS ARE STANDARD
003100     BLOCK CONTAINS 0 RECORDS
003200     RECORDING MODE IS F.
003300 01  EMPLOYEE-RECORD            PIC X(80).
003400 FD  REPORT-FILE
003500     LABEL RECORDS ARE STANDARD
003600     BLOCK CONTAINS 0 RECORDS
003700     RECORDING MODE IS F.
003800 01  REPORT-RECORD              PIC X(80).
003900 WORKING-STORAGE SECTION.
004000 01  SWITCHES.
004100     05  JUMP-SW                PIC X        VALUE 'N'.
004200         88  JUMPING                         VALUE 'Y'.
004300     05  EOF-SW                 PIC X        VALUE 'N'.
004400         88  END-OF-FILE                     VALUE 'Y'.
004500     05  REGION-ERROR-SW        PIC X        VALUE 'N'.
004600         88  INVALID-REGION                  VALUE 'Y'.
004700         88  VALID-REGION                    VALUE 'N'.
004800     05  PARM-ERROR-SW          PIC X        VALUE 'N'.
004900         88  BAD-PARM                        VALUE 'Y'.
005000         88  GOOD-PARM                       VALUE 'N'.
005100     05  END-OF-MONTH-SW        PIC X        VALUE 'N'.
005200         88  END-OF-MONTH                    VALUE 'Y'.
005300 01  COUNTERS.
005400     05  PAGE-COUNT             PIC 9(3)     VALUE 1.
005500     05  EMP-LINE-COUNT         PIC S99      VALUE +56.
005600     05  REG-LINE-COUNT         PIC S99      VALUE +56.
005700     05  START-NUMBER           PIC 999.
005800     05  RECORDS-READ           PIC 999      VALUE 0.
005900     05  NORTH-COUNT            PIC 9(2)     VALUE 0.
006000     05  SOUTH-COUNT            PIC 9(2)     VALUE 0.
006100     05  EAST-COUNT             PIC 9(2)     VALUE 0.
006200     05  WEST-COUNT             PIC 9(2)     VALUE 0.
006300 01  REGION-SUB                 PIC 9        VALUE 0.
006400 01  YRS-OF-SERVICE             PIC 99       VALUE 0.
006500 01  TODAYS-DATE                PIC X(6).
006600 01  DATE-FIELDS REDEFINES TODAYS-DATE.
006700     05  DATE-YY                PIC 9(2).
006800     05  DATE-MM                PIC 9(2).
006900     05  DATE-DD                PIC 9(2).
007000**
007100**  HOLD EMPLOYEE DETAIL PRINT LINES UNTIL READY TO PRINT
007200**  EMPLOYEE COMPENSATION REPORT.  THE DATA IS STORED BY
007300**  REGION AND THEN BY SEQUENCE IN EMPLOYEE FILE.
007400**
007500 COPY HOLDTB.
007600
007700**
007800**  STORES THE NAME OF EACH REGION
007900**
008000 01  REGION-NAME-TABLE.
008100     05  FILLER            PIC X(5)    VALUE 'NORTH'.
008200     05  FILLER            PIC X(5)    VALUE 'SOUTH'.
008300     05  FILLER            PIC X(5)    VALUE 'EAST '.
008400     05  FILLER            PIC X(5)    VALUE 'WEST '.
008500 01  REGION-TABLE     REDEFINES REGION-NAME-TABLE.
008600     05  REGION-ID         PIC X(5)  OCCURS 4 TIMES.
008700**
008800**  STORES REGIONAL INFORMATION THAT IS USED TO PRINT THE
008900**  REGIONAL SALES REPORT.  REGION SALES IS A SUM OF ALL SALES
009000**  FOR THE REGION AND IS USED TO CALCULATE MANAGER COMMISSION
009100**  THE COMMENT FIELD IS USED TO FLAG A REGION IF AN EMPLOYEE IN
009200**  THE REGION HAS 0 SALES.
009300**
009400 01  REGION-SALES-TABLE.
009500     05  REGION-DATA         OCCURS 4 TIMES.
009600         10  REGION-NAME       PIC X(5).
009700         10  REGION-MANAGER    PIC X(15).
009800         10  REGION-SALARY     PIC 9(4)V99.
009900         10  REGION-SALES      PIC 9(6)V99.
010000         10  REGION-COMMENT    PIC X(5).
010100**
010200**  FIELDS USED BY CALLED PROGRAM CWXTSUBC TO CALCULATE
010300**  COMMISSION BASED ON SALES AMOUNT
010400**
010500 01  CALC-COMMISSION-FIELDS.
010600     05  EMP-TYPE              PIC X.
010700     05  CALC-SALES            PIC 9(6)V99           VALUE 0.
010800     05  CALC-COMMISSION       PIC 9(5)V99  COMP-3   VALUE 0.
010900**
011000**  ACCUMULATORS USED FOR CALCULATING HOURLY EMPLOYEE WAGES,
011100**  TOTAL EMPLOYEE COMPENSATION (SALARY PLUS COMMISSION OR
011200**  HOURLY EMPLOYEE WAGES PLUS OVERTIME), AND TOTAL MANAGEMENT
011300**  COMPENSATION (SALARY PLUS COMMISSION BASED ON TOTAL SALES
011400**  FOR THE REGION)
011500**
011600 01  TOTAL-FIELDS.
011700     05  EMP-WAGES             PIC 9(5)V99    COMP-3.
011800     05  EMP-COMPENSATION      PIC 9(5)V99    COMP-3.
011900     05  MGMT-COMPENSATION     PIC 9(5)V99    COMP-3.
012000**
012100**  TOTAL COMPENSATION GIVEN TO ALL EMPLOYEES (HOURLY AND SALES)
012200**  OR MANAGEMENT.  EACH SUM IS PRINTED AT THE END OF THEIR
012300**  RESPECTIVE REPORTS.
012400**
012500 01  GRAND-TOTAL-FIELDS.
012600     05  GRAND-TOTAL-EMP       PIC 9(7)V99   COMP-3  VALUE 0.
012700     05  GRAND-TOTAL-MGMT      PIC 9(7)V99   COMP-3  VALUE 0.
012800**
012900**  USED FOR CALCULATING OVERTIME FOR ANY HOURLY EMPLOYEE
013000**  WHOSE HOURS EXCEEDS 40
013100**
013200 01  OVERTIME-FIELDS.
013300     05  OT-AMOUNT             PIC 9(5)V99    COMP-3.
013400     05  OT-HOURS              PIC 9(2).
013500**
013600**  EMPLOYEE RECORD WORK-AREA.  EMPLOYEE DATA IS REDEFINED
013700**  BASED ON ONE OF THE 3 EMPLOYEE TYPES, HOURLY, SALES OR
013800**  MANAGEMENT.
013900**
014000 COPY CWXTEMPF.
014100
014200***
014300***  EMPLOYEE COMPENSATION REPORT
014400***
014500 COPY CWXTEMPR.
014600***
014700***  REGIONAL SALES REPORT
014800***
014900 COPY CWXTRGNR.
015000***
015100***  NOT USED VARIABLE
015200***
015300 01  NOT-USED               PIC X(1).
015000***
015100***  ERROR MESSAGE LINE
015200***
015300 01  ERROR-LINE             PIC X(80).
015400***
015500***  BLANK LINE TO CONTROL SPACING OF REPORTS
015600***
015700 01  BLANK-LINE             PIC X(80)   VALUE SPACES.
015800***
015900***  PARM IS AN OPTIONAL FIELD USED TO START PROCESSING
016000***  AT A PARTICULAR RECORD IN THE EMPLOYEE FILE.  VALID
016100***  VALUES FOR PARM-DATA ARE:
016200***
016300***        VALUE           FUNCTION
016400***     - SPACES OR 00001  BEGIN PROCESSING FROM FIRST RECORD.
016500***                          CAUSES S0C7 ABEND BECAUSE FIRST
016600***                          RECORD CONTAINS INVALID DATA.
016700***     - 00002            BEGIN PROCESSING FROM SECOND RECORD.
016800***                          CAUSES S0C7 ABEND BECAUSE SECOND
016900***                          RECORD CONTAINS INVALID REGION
017000***                          CREATING TABLE OVERFLOW CONDITION.
017100***     - 00003 THRU 00019 BEGIN PROCESSING AT SPECIFIED RECORD
017200***                          UNTIL END-OF-FILE.  BYPASSES S0C7
017300***                          CAUSED BY RECORDS 00001 AND 00002.
017400***     - 00016 THRU 00019 PROCESS MANAGEMENT RECORDS AND PRINT
017500***                          REGIONAL SALES REPORT ONLY.  IF
017600***                          PARM > 16, ONE OR MORE MANAGER NAMES
017700***                          WILL NOT APPEAR ON THE REPORT.
017800***     - 00020 OR ABOVE   PRINTS MANAGEMENT REPORT WITHOUT
017900***                          MANAGER NAMES AND ZERO VALUES.
018000***     - NON NUMERIC OR   PRINTS ERROR MESSAGE AND SKIPS ALL
018100***         < 5 CHARACTERS   PROCESSING.
018200***
018300 LINKAGE SECTION.
018400 01  PARMINFO.
018500     03  PARM-LTH           PIC S9(4) COMP.
018600     03  PARM-DATA          PIC X(5).
018700 PROCEDURE DIVISION USING PARMINFO.
018800 0000-MAINLINE.
018900     PERFORM 9000-OPEN.
019000     PERFORM 9100-CHECK-PARM.
019100     IF GOOD-PARM
019200        PERFORM 9200-INIT
019300        PERFORM 8000-READ-INPUT
019400             UNTIL RECORDS-READ = START-NUMBER
019500             OR END-OF-FILE
019600        PERFORM 1000-PROCESS-DATA
019700              UNTIL END-OF-FILE
019800        PERFORM 6000-PRINT-EMPLOYEE-REPORT
019900        MOVE 1 TO REGION-SUB
020000        IF END-OF-MONTH
020100            PERFORM 7000-PRINT-REGION-REPORT
020200                  UNTIL REGION-SUB > 4.
020300     PERFORM 9900-CLOSE.
020400     GOBACK.
020500***
020600***  DIFFERENT PROCESSING OCCURS BASED ON EMPLOYEE TYPE.  THERE
020700***  ARE 3 VALID EMPLOYEE TYPES.  IF A RECORD IS READ CONTAINING
020800***  A RECORD TYPE OTHER THAN H, S OR M, AN ERROR MESSAGE IS
020900***  WRITTEN AND PROCESSING CONTINUES.
021000***
021100 1000-PROCESS-DATA.
021200     IF HOURLY
021300         PERFORM 2000-PROCESS-HOURLY
021400     ELSE
021500         IF SALES
021600             PERFORM 3000-PROCESS-SALES
021700         ELSE
021800             IF MANAGEMENT
021900                 PERFORM 4000-PROCESS-MANAGEMENT
022000             ELSE
022100                 MOVE ' INVALID EMPLOYEE TYPE ' TO ERROR-LINE
022200                 WRITE REPORT-RECORD FROM ERROR-LINE.
022300***                       GO TO 9999-CAN-DELETE.
022400     PERFORM 8000-READ-INPUT.
022500***
022600***  CALCULATE TYPE H (HOURLY) EMPLOYEE COMPENSATION.  ANY
022700***  EMPLOYEE WITH MORE THAN 40 HOURS RECEIVES OVERTIME COMPUTED
022800***  AT 1.5 TIMES THEIR HOURLY RATE.  ONCE EMPLOYEE COMPENSATION
022900***  IS CALCULATED, IT IS STORED IN A HOLD TABLE.  THE DATA IN
023000***  THE HOLD TABLE IS USED FOR PRINTING THE EMPLOYEE COMPENSATION
023100***  REPORT.
023200***
023300 2000-PROCESS-HOURLY.
023400     MOVE ZERO TO OT-AMOUNT.
023500     IF WA-EMP-HOURS GREATER THAN 40
023600         COMPUTE EMP-WAGES = WA-EMP-RATE * 40
023700         COMPUTE OT-HOURS  = WA-EMP-HOURS - 40
023800         COMPUTE OT-AMOUNT = OT-HOURS * (WA-EMP-RATE * 1.5)
023900     ELSE
024000         COMPUTE EMP-WAGES = WA-EMP-HOURS * WA-EMP-RATE.
024100     COMPUTE EMP-COMPENSATION = EMP-WAGES + OT-AMOUNT.
024200     ADD EMP-COMPENSATION TO GRAND-TOTAL-EMP.
024300     CALL 'CWXTDATE' USING END-OF-MONTH-SW
024400                           YRS-OF-SERVICE
024500                           TODAYS-DATE
024600                           WA-EMP-HIRE-DATE.
024700     PERFORM 5000-STORE-EMPLOYEE-DETAIL.
024800     SET HOLD-IX UP BY 1.
024900***
025000***  CALCULATE TYPE S (SALES) EMPLOYEE COMPENSATION.  THE TOTAL
025100***  SALES FOR THE EMPLOYEE IS PASSED TO THE CALLED PROGRAM WHICH
025200***  CALCULATES COMMISSION.  ONCE EMPLOYEE COMPENSATION IS
025300***  RETURNED FROM CWXTSUBC, IT IS STORED IN A HOLD-TABLE.  THE
025400***  DATA IN THE HOLD-TABLE IS USED FOR PRINTING THE EMPLOYEE
025500***  COMPENSATION REPORT.
025600***
025700 3000-PROCESS-SALES.
025800     IF WA-SALES-AMOUNT > 0
025900        ADD  WA-SALES-AMOUNT  TO REGION-SALES (WA-EMP-REGION)
026000        MOVE WA-SALES-AMOUNT  TO CALC-SALES
026100        MOVE 0 TO CALC-COMMISSION
026200        MOVE 'S' TO EMP-TYPE
026300        CALL 'CWXTSUBC' USING EMP-TYPE,
026400                              CALC-SALES,
026500                              CALC-COMMISSION
026600     ELSE
026700        MOVE 'UH-OH' TO REGION-COMMENT (WA-EMP-REGION).
026800     COMPUTE EMP-COMPENSATION = WA-SALES-SALARY +
026900                                CALC-COMMISSION.
027000     ADD  EMP-COMPENSATION TO GRAND-TOTAL-EMP.
027100     CALL 'CWXTDATE' USING END-OF-MONTH-SW
027200                           YRS-OF-SERVICE
027300                           TODAYS-DATE
027400                           WA-EMP-HIRE-DATE.
027500     PERFORM 5000-STORE-EMPLOYEE-DETAIL.
027600     SET HOLD-IX UP BY 1.
027700***
027800***  PROCESS TYPE M (MANAGEMENT) RECORDS.  THE MANAGER NAME AND
027900***  SALARY ARE STORED IN A TABLE FOR USE DURING PRINTING OF THE
028000***  REGIONAL SALES REPORT.
028100***
028200 4000-PROCESS-MANAGEMENT.
028300     MOVE WA-EMP-NAME    TO REGION-MANAGER (WA-EMP-REGION).
028400     MOVE WA-MGMT-SALARY TO REGION-SALARY (WA-EMP-REGION).
028500***
028600***  SALES AND HOURLY EMPLOYEE DATA IS STORED IN A HOLD TABLE FOR
028700***  PRINTING OF EMPLOYEE COMPENSATION REPORT.  THE HOLD TABLE IS
028800***  A TWO-DIMENSIONAL TABLE AND HOLDS DATA FOR A MAXIMUM OF 20
028900***  EMPLOYEES.
029000***
029100 5000-STORE-EMPLOYEE-DETAIL.
029200     PERFORM 5100-SET-INDEX.
029300     IF VALID-REGION
029400         MOVE WA-EMP-NAME TO HOLD-NAME (REG-IX, HOLD-IX)
029500         MOVE REGION-ID (WA-EMP-REGION)
029600                          TO HOLD-REGION (REG-IX, HOLD-IX)
029700         MOVE WA-EMP-TYPE TO HOLD-TYPE (REG-IX, HOLD-IX)
029800         MOVE WA-EMP-HIRE-DATE TO HOLD-HIRE-DATE (REG-IX, HOLD-IX)
029900         MOVE YRS-OF-SERVICE TO HOLD-YEARS (REG-IX, HOLD-IX)
030000         MOVE EMP-COMPENSATION
030100                          TO HOLD-TOTAL (REG-IX, HOLD-IX)
030200         IF HOURLY
030300            MOVE EMP-WAGES TO HOLD-WAGES (REG-IX, HOLD-IX)
030400            MOVE OT-AMOUNT TO HOLD-OT (REG-IX, HOLD-IX)
030500            MOVE ZEROS     TO HOLD-COMM (REG-IX, HOLD-IX)
030600         ELSE
030700            MOVE WA-SALES-SALARY
030800                           TO HOLD-WAGES(REG-IX, HOLD-IX)
030900            MOVE CALC-COMMISSION
031000                           TO HOLD-COMM (REG-IX, HOLD-IX)
031100            MOVE ZERO      TO HOLD-OT   (REG-IX, HOLD-IX).
031200***
031300***  SET THE REGION INDEX BASED ON EMPLOYEE REGION ID AND
031400***  SEQUENTIALLY INCREMENT HOLD INDEX WITHIN EACH REGION.
031500***  THE EMPLOYEE COMPENSATION REPORT WILL BE GROUPED BY
031600***  REGION.  IF AN INVALID REGION IS FOUND, AN ERROR MESSAGE
031700***  IS WRITTEN AND PROCESSING CONTINUES.
031800***
031900 5100-SET-INDEX.
032000     MOVE 'N' TO REGION-ERROR-SW.
032100     IF NORTH
032200         ADD 1 TO NORTH-COUNT
032300         SET HOLD-IX TO NORTH-COUNT
032400     ELSE
032500         IF SOUTH
032600             ADD 1 TO SOUTH-COUNT
032700             SET HOLD-IX TO SOUTH-COUNT
032800         ELSE
032900             IF EAST
033000                 ADD 1 TO EAST-COUNT
033100                 SET HOLD-IX TO EAST-COUNT
033200             ELSE
033300                 IF WEST
033400                     ADD 1 TO WEST-COUNT
033500                     SET HOLD-IX TO WEST-COUNT
033600                 ELSE
033700                     MOVE 'Y' TO REGION-ERROR-SW.
033800     IF VALID-REGION
033900         SET REG-IX TO WA-EMP-REGION.
034000***
034100***  COMPENSATION DATA FOR HOURLY AND SALES EMPLOYEES ARE PRINTED
034200***  TO THE EMPLOYEE COMPENSATION REPORT FROM THE HOLD TABLE.
034300***
034400 6000-PRINT-EMPLOYEE-REPORT.
034500     SET REG-IX TO 1.
034600     PERFORM 6100-PRINT-EMPLOYEE-DETAIL
034700           VARYING HOLD-IX FROM 1 BY 1
034800           UNTIL HOLD-IX > NORTH-COUNT.
034900     SET REG-IX TO 2.
035000     PERFORM 6100-PRINT-EMPLOYEE-DETAIL
035100           VARYING HOLD-IX FROM 1 BY 1
035200           UNTIL HOLD-IX > SOUTH-COUNT.
035300     SET REG-IX TO 3.
035400     PERFORM 6100-PRINT-EMPLOYEE-DETAIL
035500           VARYING HOLD-IX FROM 1 BY 1
035600           UNTIL HOLD-IX > EAST-COUNT.
035700     SET REG-IX TO 4.
035800     PERFORM 6100-PRINT-EMPLOYEE-DETAIL
035900           VARYING HOLD-IX FROM 1 BY 1
036000           UNTIL HOLD-IX > WEST-COUNT.
036100     WRITE REPORT-RECORD FROM BLANK-LINE.
036200     MOVE GRAND-TOTAL-EMP TO EMP-GRAND-TOTAL.
036300     WRITE REPORT-RECORD FROM EMP-TOTAL-DTL.
036400***
036500***  PRINT DETAIL LINES FOR EMPLOYEE COMPENSATION REPORT
036600***
036700 6100-PRINT-EMPLOYEE-DETAIL.
036800     IF EMP-LINE-COUNT GREATER THAN 55
036900           PERFORM 6200-PRINT-EMPLOYEE-HEADERS.
037000     MOVE HOLD-NAME   (REG-IX, HOLD-IX) TO EMP-DTL-NAME.
037100     MOVE HOLD-REGION (REG-IX, HOLD-IX) TO EMP-DTL-REGION.
037200     MOVE HOLD-TYPE   (REG-IX, HOLD-IX) TO EMP-DTL-TYPE.
037300     MOVE HOLD-HIRE-MM(REG-IX, HOLD-IX) TO EMP-DTL-HIRE-MM.
037400     MOVE '/'                           TO EMP-DTL-SLASH1.
037500     MOVE HOLD-HIRE-DD(REG-IX, HOLD-IX) TO EMP-DTL-HIRE-DD.
037600     MOVE '/'                           TO EMP-DTL-SLASH2.
037700     MOVE HOLD-HIRE-YY(REG-IX, HOLD-IX) TO EMP-DTL-HIRE-YY.
037800     MOVE HOLD-YEARS  (REG-IX, HOLD-IX) TO EMP-DTL-YRS-OF-SERVICE.
037900     MOVE HOLD-WAGES  (REG-IX, HOLD-IX) TO EMP-DTL-WAGES.
038000     MOVE HOLD-OT     (REG-IX, HOLD-IX) TO EMP-DTL-OT.
038100     MOVE HOLD-COMM   (REG-IX, HOLD-IX) TO EMP-DTL-COMM.
038200     MOVE HOLD-TOTAL  (REG-IX, HOLD-IX) TO EMP-DTL-TOTAL.
038300     WRITE REPORT-RECORD FROM EMPLOYEE-DTL
038400       AFTER ADVANCING 1 LINE.
038500     ADD  1 TO EMP-LINE-COUNT.
038600     MOVE SPACES TO EMPLOYEE-DTL.
038700***
038800***  PRINT HEADERS FOR EMPLOYEE COMPENSATION REPORT
038900***
039000 6200-PRINT-EMPLOYEE-HEADERS.
039100         MOVE PAGE-COUNT TO EMP-PAGE.
039200         MOVE DATE-YY TO EMP-RUN-YY.
039300         MOVE DATE-MM TO EMP-RUN-MM.
039400         MOVE DATE-DD TO EMP-RUN-DD.
039500         WRITE REPORT-RECORD FROM BLANK-LINE.
039600         WRITE REPORT-RECORD FROM BLANK-LINE.
039700         WRITE REPORT-RECORD FROM BLANK-LINE.
039800         WRITE REPORT-RECORD FROM EMPLOYEE-HDR1.
039900         WRITE REPORT-RECORD FROM BLANK-LINE.
040000         WRITE REPORT-RECORD FROM BLANK-LINE.
040100         WRITE REPORT-RECORD FROM EMPLOYEE-HDR2.
040200         WRITE REPORT-RECORD FROM EMPLOYEE-HDR3.
040300         WRITE REPORT-RECORD FROM BLANK-LINE.
040400         ADD 1 TO PAGE-COUNT.
040500         MOVE 5 TO EMP-LINE-COUNT.
040600***
040700***  COMPENSATION DATA FOR MANAGEMENT EMPLOYEES IS PRINTED
040800***  TO THE REGIONAL SALES REPORT.  THE TOTAL SALES FOR THE
040900***  REGION IS PASSED TO THE CALLED PROGRAM WHICH CALCULATES
041000***  COMMISSION.  THIS REPORT IS GENERATED AT END-OF-MONTH.
041100***
041200 7000-PRINT-REGION-REPORT.
041300     IF REG-LINE-COUNT GREATER THAN 55
041400         PERFORM 7100-PRINT-REGION-HEADERS.
041500     MOVE SPACES TO REGION-DETAIL.
041600     MOVE REGION-NAME    (REGION-SUB) TO REG-DTL-REGION.
041700     MOVE REGION-MANAGER (REGION-SUB) TO REG-DTL-MANAGER.
041800     MOVE REGION-SALARY  (REGION-SUB) TO REG-DTL-SALARY.
041900     MOVE REGION-SALES   (REGION-SUB) TO REG-DTL-SALES.
042000     MOVE REGION-SALES   (REGION-SUB) TO CALC-SALES.
042100     MOVE REGION-COMMENT (REGION-SUB) TO REG-DTL-COMMENT.
042200     MOVE 'M' TO EMP-TYPE.
042300     CALL 'CWXTSUBC' USING EMP-TYPE,
042400                           CALC-SALES,
042500                           CALC-COMMISSION.
042600     MOVE CALC-COMMISSION TO REG-DTL-COMM.
042700     COMPUTE MGMT-COMPENSATION = CALC-COMMISSION +
042800                                REGION-SALARY(REGION-SUB).
042900     ADD  MGMT-COMPENSATION TO GRAND-TOTAL-MGMT.
043000     MOVE MGMT-COMPENSATION TO REG-DTL-TOTAL.
043100     WRITE REPORT-RECORD FROM REGION-DETAIL.
043200     IF REGION-SUB = 4
043300        WRITE REPORT-RECORD FROM BLANK-LINE
043400        MOVE GRAND-TOTAL-MGMT TO MGMT-GRAND-TOTAL
043500        WRITE REPORT-RECORD FROM MGMT-TOTAL-DTL.
043600     ADD 1 TO REG-LINE-COUNT.
043700     ADD 1 TO REGION-SUB.
043800***
043900***  PRINT HEADERS FOR REGIONAL SALES REPORT
044000***
044100 7100-PRINT-REGION-HEADERS.
044200         MOVE PAGE-COUNT TO REG-PAGE.
044300         MOVE DATE-YY TO REG-RUN-YEAR.
044400         MOVE DATE-MM TO REG-RUN-MONTH.
044500         MOVE DATE-DD TO REG-RUN-DAY.
044600         WRITE REPORT-RECORD FROM BLANK-LINE.
044700         WRITE REPORT-RECORD FROM BLANK-LINE.
044800         WRITE REPORT-RECORD FROM BLANK-LINE.
044900         WRITE REPORT-RECORD FROM BLANK-LINE.
045000         WRITE REPORT-RECORD FROM BLANK-LINE.
045100         WRITE REPORT-RECORD FROM REGION-HDR1.
045200         WRITE REPORT-RECORD FROM BLANK-LINE.
045300         WRITE REPORT-RECORD FROM BLANK-LINE.
045400         WRITE REPORT-RECORD FROM REGION-HDR2.
045500         WRITE REPORT-RECORD FROM BLANK-LINE.
045600         ADD 1 TO PAGE-COUNT.
045700         MOVE 4 TO REG-LINE-COUNT.
045800***
045900***
046000 8000-READ-INPUT.
046100     READ EMPLOYEE-FILE INTO EMPLOYEE-WORK-AREA
046200         AT END
046300              MOVE 'Y' TO EOF-SW.
046400     ADD 1 TO RECORDS-READ.
046500***
046600***
046700 9000-OPEN.
046800     OPEN INPUT  EMPLOYEE-FILE.
046900     OPEN OUTPUT REPORT-FILE.
047000***
047100***  VALID PARMS ARE BLANK OR 5 DIGIT NUMBER
047200***
047300 9100-CHECK-PARM.
047400     IF PARM-LTH = 5
047500         IF PARM-DATA IS NUMERIC
047600             MOVE PARM-DATA TO START-NUMBER
047700         ELSE
047800             PERFORM 9800-BAD-PARM
047900     ELSE
048000         IF PARM-LTH = 0
048100             MOVE 1 TO START-NUMBER
048200         ELSE
048300             PERFORM 9800-BAD-PARM.
048400***
048500***
048600 9200-INIT.
048700     MOVE 'NORTH' TO REGION-NAME (1).
048800     MOVE 'SOUTH' TO REGION-NAME (2).
048900     MOVE 'EAST ' TO REGION-NAME (3).
049000     MOVE 'WEST ' TO REGION-NAME (4).
049100     MOVE 1 TO REGION-SUB.
049200     PERFORM 9300-INITIALIZE-REGION-TABLE
049300        UNTIL REGION-SUB > 4.
049400     SET HOLD-IX TO 1.
049500     ACCEPT TODAYS-DATE FROM DATE.
049600     CALL 'CWXTDATE' USING END-OF-MONTH-SW
049700                           YRS-OF-SERVICE
049800                           TODAYS-DATE
049900                           WA-EMP-HIRE-DATE.
050000***
050100***
050200 9300-INITIALIZE-REGION-TABLE.
050300     MOVE SPACES TO REGION-MANAGER (REGION-SUB).
050400     MOVE SPACES TO REGION-COMMENT (REGION-SUB).
050500     MOVE 0 TO REGION-SALARY (REGION-SUB).
050600     MOVE 0 TO REGION-SALES  (REGION-SUB).
050700     ADD  1 TO REGION-SUB.
050800***
050900***
051000 9800-BAD-PARM.
051100     MOVE 'Y' TO PARM-ERROR-SW.
051200     MOVE '   PARAMETER LENGTH OR DATA IS INCORRECT   '
051300         TO ERROR-LINE.
051400     WRITE REPORT-RECORD FROM ERROR-LINE.
051500***
051600***
051700 9900-CLOSE.
051800     CLOSE EMPLOYEE-FILE.
051900     CLOSE REPORT-FILE.
052000***
052100***
052200 9999-RIP.
052300     DISPLAY '    ************    '.
052400     DISPLAY '   *            *   '.
052500     DISPLAY '  *   T H I S    *  '.
052600     DISPLAY ' *                * '.
052700     DISPLAY ' *      I S       * '.
052800     DISPLAY ' *                * '.
052900     DISPLAY ' *    D E A D     * '.
053000     DISPLAY ' *                * '.
053100     DISPLAY ' *    C O D E     * '.
053200     DISPLAY ' ****************** '.